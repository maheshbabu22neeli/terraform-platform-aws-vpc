resource "aws_vpc_peering_connection" "default" {

  count = var.is_peering_required ? 1 : 0

  // acceptor or aws default vpc id
  peer_vpc_id = data.aws_vpc.default.id

  // Requestor or our VPC id
  vpc_id = aws_vpc.main.id

  auto_accept = true

  accepter {
    allow_remote_vpc_dns_resolution = true
  }

  requester {
    allow_remote_vpc_dns_resolution = true
  }

  tags = merge(
    local.common_tags,
    var.peering_vpc_tags,
    {
      // Peering connection name = roboshop-dev-default
      Name = "${var.project}-${var.environment}-default"
    }
  )
}

// this is at roboshop-dev side routes, adding default vpc details here
resource "aws_route" "public_peering" {

  count = var.is_peering_required ? 1 : 0

  route_table_id            = aws_route_table.public.id
  destination_cidr_block    = data.aws_vpc.default.cidr_block
  vpc_peering_connection_id = aws_vpc_peering_connection.default[count.index].id
}

resource "aws_route" "private_peering" {

  count = var.is_peering_required ? 1 : 0

  route_table_id            = aws_route_table.private.id
  destination_cidr_block    = data.aws_vpc.default.cidr_block
  vpc_peering_connection_id = aws_vpc_peering_connection.default[count.index].id
}

resource "aws_route" "database_peering" {

  count = var.is_peering_required ? 1 : 0

  route_table_id            = aws_route_table.database.id
  destination_cidr_block    = data.aws_vpc.default.cidr_block
  vpc_peering_connection_id = aws_vpc_peering_connection.default[count.index].id
}

// this is at default vpc side, adding roboshop vpc id details
resource "aws_route" "default_peering" {

  count = var.is_peering_required ? 1 : 0

  route_table_id            = data.aws_route_table.default.id
  destination_cidr_block    = var.vpc_cider
  vpc_peering_connection_id = aws_vpc_peering_connection.default[count.index].id
}